/**
 * This prototype handles all assets of a location (Body ||Â Head) set via Settings.yaml
 */

prototype(Carbon.IncludeAssets:Internal.Assets) < prototype(Neos.Fusion:Component) {
    // Body || Head
    location = null

    contexts = ${['General', 'Backend', 'Live']}
    loadJSforCSSAsynchron = ${Configuration.setting('Carbon.IncludeAssets.LoadJSforCSSAsynchron') && this.location == 'Head'}
    packages = ${Configuration.setting('Carbon.IncludeAssets.Packages')}
    packages.@process.sort = ${Type.isArray(value) ? Carbon.Array.ksort(value) : value}

    @if.hasLocation = ${this.location}

    renderer = Neos.Fusion:Component {
        inHead = ${props.location == 'Head'}
        filesFromPackages = afx`
            <Neos.Fusion:Collection @if.hasPackages={props.packages} collection={props.packages} @children='itemRenderer'>
                <Carbon.IncludeAssets:Internal.Package location={props.location} config={item} />
            </Neos.Fusion:Collection>
        `
        loadAsyncCSS = ${props.loadJSforCSSAsynchron && String.indexOf(this.filesFromPackages, '<link rel="preload" as="style" onload="this.onload=null;') >= 0}

        renderer = afx`
            <Carbon.IncludeAssets:ResourceHints @if.head={props.inHead} />
            <Carbon.IncludeAssets:GoogleFonts @if.head={props.inHead} />
            {props.filesFromPackages}
            <Carbon.IncludeAssets:File @if.render={props.loadAsyncCSS} file='resource://Carbon.IncludeAssets/Private/Templates/LoadCssAsync.js[inline]' />
        `
    }
}

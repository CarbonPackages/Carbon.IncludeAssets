prototype(Carbon.IncludeAssets:Asset) < prototype(Neos.Fusion:Value) {
	// General || Live || Backend
	context = 'General'
	// Body || Head
	location = 'Body'

	@context {
		context = ${this.context}
		location = ${this.location}
		config = ${Configuration.setting('Carbon.IncludeAssets')}
	}

	value = Neos.Fusion:Array {
		styles = Carbon.IncludeAssets:StyleCollection {
			collection = ${String.split(config[context][location].Style, ',')}
		}
		scripts = Carbon.IncludeAssets:ScriptCollection {
			collection = ${String.split(config[context][location].Script, ',')}
		}
	}

	@if.hasContext = Neos.Fusion:Case {
		inBackend {
			condition = ${context == 'Backend'}
			renderer = ${node.context.inBackend}
		}

		isLive {
			condition = ${context == 'Live'}
			renderer = ${node.context.live}
		}

		default {
			condition = true
			renderer = true
		}
	}
}

prototype(Carbon.IncludeAssets:Assets)  < prototype(Neos.Fusion:Value) {
	// Body || Head
	location = 'Body'
	@context.location = ${this.location}

	value = Neos.Fusion:Array {
		general = Carbon.IncludeAssets:Asset {
			context = 'General'
			location = ${location}
		}
		inBackend = Carbon.IncludeAssets:Asset {
			context = 'Backend'
			location = ${location}
		}
		live = Carbon.IncludeAssets:Asset {
			context = 'Live'
			location = ${location}
		}
	}

	@position = 'start'
}
